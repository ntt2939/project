#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <Wire.h>
#include <SHTSensor.h>
#include <ModbusIP_ESP8266.h>

#define I2C_SDA_PIN D2
#define I2C_SCL_PIN D1

const char* ssid     = "iPhone";
const char* password = "123456789";

const uint16_t REG_TEMPERATURE = 0;  // thanh ghi 0-1
const uint16_t REG_HUMIDITY    = 2;  // thanh ghi 2-3

SHTSensor sht;
ModbusIP mb;

unsigned long lastMeasureTime = 0;
unsigned long lastMixingTime = 0;
unsigned long mixingStartTime = 0;

const unsigned long INTERVAL_NORMAL = 18000;
const unsigned long INTERVAL_MIXING = 600;
const unsigned long MIXING_DURATION = 6000;
const unsigned long MIXING_CYCLE    = 288000;

bool mixingActive = false;

union FloatUnion {
  float value;
  uint8_t bytes[4];
};

void setup() {
  Serial.begin(9600);
  Wire.begin(I2C_SDA_PIN, I2C_SCL_PIN);

  if (!sht.init()) {
    Serial.println("Không tìm thấy cảm biến SHT85");
    while (1) delay(1000);
  }

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  Serial.print("Kết nối WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }
  Serial.println("\nĐã kết nối WiFi");
  Serial.print("IP: ");
  Serial.println(WiFi.localIP());

  mb.server();

  mb.onConnect([](IPAddress ip) {
    Serial.print("Client Modbus kết nối: ");
    Serial.println(ip);
    return true;
  });

  // Khởi tạo 4 thanh ghi: 0-1 cho nhiệt độ, 2-3 cho độ ẩm
  for (int i = 0; i < 4; i++) {
    mb.addHreg(i, 0);
  }

  lastMeasureTime = millis();
  lastMixingTime = millis();
}

void loop() {
  mb.task();

  unsigned long now = millis();

  if (!mixingActive && (now - lastMixingTime >= MIXING_CYCLE)) {
    mixingActive = true;
    mixingStartTime = now;
    lastMixingTime = now;
    lastMeasureTime = 0;
    Serial.println("Bắt đầu đảo trộn");
  }

  if (mixingActive && (now - mixingStartTime >= MIXING_DURATION)) {
    mixingActive = false;
    lastMeasureTime = now;
    Serial.println("Kết thúc đảo trộn");
  }

  unsigned long interval = mixingActive ? INTERVAL_MIXING : INTERVAL_NORMAL;

  if (now - lastMeasureTime >= interval) {
    if (sht.readSample()) {
      float temp = sht.getTemperature();
      float hum  = sht.getHumidity() * 0.82;

      // Chuyển float sang 4 byte
      FloatUnion tempUnion, humUnion;
      tempUnion.value = temp;
      humUnion.value = hum;

  
      uint16_t temp_high = (tempUnion.bytes[0] << 8) | tempUnion.bytes[1];
      uint16_t temp_low  = (tempUnion.bytes[2] << 8) | tempUnion.bytes[3];

      uint16_t hum_high  = (humUnion.bytes[0] << 8) | humUnion.bytes[1];
      uint16_t hum_low   = (humUnion.bytes[2] << 8) | humUnion.bytes[3];

      mb.Hreg(REG_TEMPERATURE, temp_high);
      mb.Hreg(REG_TEMPERATURE + 1, temp_low);

      mb.Hreg(REG_HUMIDITY, hum_high);
      mb.Hreg(REG_HUMIDITY + 1, hum_low);

      Serial.printf("Gửi Modbus REAL - Temp: %.2f °C | Humi: %.2f %%\n", temp, hum);
    } else {
      Serial.println("Lỗi đọc cảm biến");
    }
    lastMeasureTime = now;
  }
}
